plugins {
    id 'groovy'
    id 'java'
    id 'io.micronaut.library' version '1.3.2'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

def agentVersion = System.getenv('RELEASE_VERSION') ?: '1.0-SNAPSHOT'

group 'dev.techh'
version agentVersion

repositories {
    mavenCentral()
}

def jUnitVersion = '5.8.1'
def jacksonVersion = '2.13.0'

micronaut {
    version = "3.2.0"
}
dependencies {

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"

    implementation 'org.javassist:javassist:3.28.0-GA'

    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-classic:1.2.7'

    implementation 'com.github.spullara.mustache.java:compiler:0.9.10'

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withSourcesJar()
}

shadowJar {

    archiveClassifier.set('')

    manifest {
        attributes 'Premain-Class': 'dev.techh.perfunit.PerfUnit'
        attributes 'Can-Retransform-Classes': true
        attributes 'Boot-Class-Path' : "perfunit-${agentVersion}.jar"
    }

    exclude '**/javax.servlet.ServletContainerInitializer'
    exclude '**/logback.xml'
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'

    jvmArgs = ["-javaagent:./build/libs/perfunit-${version}.jar=./src/test/resources/config-sample.yml"]
    useJUnitPlatform()
}
