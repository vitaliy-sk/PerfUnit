plugins {
    id 'groovy'
    id 'java'
}

def agentVersion = '1.0-SNAPSHOT'

group 'dev.techh'
version agentVersion

repositories {
    mavenCentral()
}

def jUnitVersion = '5.8.1'
def jacksonVersion = '2.13.0'

dependencies {

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"

    implementation 'org.javassist:javassist:3.28.0-GA'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-classic:1.2.7'

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

}

jar {
    manifest {
        attributes 'Premain-Class': 'dev.techh.PerfUnit'
        attributes 'Can-Retransform-Classes': true
        attributes 'Boot-Class-Path' : "perfunit-${agentVersion}.jar"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'

    jvmArgs = ["-javaagent:./build/libs/perfunit-${version}.jar=./src/test/resources/config-sample.yml"]
    useJUnitPlatform()
}
